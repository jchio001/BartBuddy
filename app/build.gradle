apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    def versionMajor = 3
    def versionMinor = 1
    def versionPatch = 0

    def baseApplicationId = "com.app.jonathan.willimissbart"

    androidExtensions {
        experimental = true
    }

    compileSdkVersion 30

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        applicationId baseApplicationId
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 37
        versionName String.format("%d.%d.%d", versionMajor, versionMinor, versionPatch)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "BART_API_KEY", BART_API_KEY

        kotlinOptions {
            freeCompilerArgs = [
                    '-Xopt-in=kotlin.RequiresOptIn'
            ]
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField(
                    "java.util.concurrent.TimeUnit",
                    "UPDATE_TIME_UNIT",
                    "java.util.concurrent.TimeUnit.SECONDS"
            )
            buildConfigField(
                    "java.util.concurrent.TimeUnit",
                    "DELAY_TIME_UNIT",
                    "java.util.concurrent.TimeUnit.MILLISECONDS"
            )
            buildConfigField(
                    "long",
                    "MINIMUM_DELAY",
                    "300"
            )
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField(
                    "java.util.concurrent.TimeUnit",
                    "UPDATE_TIME_UNIT",
                    "java.util.concurrent.TimeUnit.MINUTES"
            )
            buildConfigField(
                    "java.util.concurrent.TimeUnit",
                    "DELAY_TIME_UNIT",
                    "java.util.concurrent.TimeUnit.MILLISECONDS"
            )
            buildConfigField(
                    "long",
                    "MINIMUM_DELAY",
                    "3"
            )
            signingConfig signingConfigs.release
        }
    }

    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/roomSchemas".toString())
        }
    }

    sourceSets {
        debug {
            java.srcDirs += 'src/debug/java'
        }
        release {
            java.srcDirs += 'src/release/java'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.3.0-rc03'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-rc03'

    implementation project(':lib-db')
    implementation project(':lib-api-models')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4.10"

    implementation libraries.dagger
    kapt libraries.daggerKapt

    implementation 'com.google.android.material:material:1.3.0-alpha03'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'

    implementation 'de.hdodenhof:circleimageview:3.0.1'

    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation libraries.roomRuntime

    implementation libraries.moshi
    implementation libraries.retrofitMoshi
    kapt libraries.moshiKapt

    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    implementation libraries.retrofit
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
}
