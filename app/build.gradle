apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    def versionMajor = 3
    def versionMinor = 0
    def versionPatch = 2

    androidExtensions {
        experimental = true
    }

    compileSdkVersion 29

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        applicationId "com.app.jonathan.willimissbart"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 28
        versionName String.format("%d.%d.%d", versionMajor, versionMinor, versionPatch)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "BART_API_KEY", BART_API_KEY
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField(
                    "java.util.concurrent.TimeUnit",
                    "UPDATE_TIME_UNIT",
                    "java.util.concurrent.TimeUnit.SECONDS"
            )
            buildConfigField(
                    "java.util.concurrent.TimeUnit",
                    "DELAY_TIME_UNIT",
                    "java.util.concurrent.TimeUnit.MILLISECONDS"
            )
            buildConfigField(
                    "long",
                    "MINIMUM_DELAY",
                    "300"
            )
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField(
                    "java.util.concurrent.TimeUnit",
                    "UPDATE_TIME_UNIT",
                    "java.util.concurrent.TimeUnit.MINUTES"
            )
            buildConfigField(
                    "java.util.concurrent.TimeUnit",
                    "DELAY_TIME_UNIT",
                    "java.util.concurrent.TimeUnit.MILLISECONDS"
            )
            buildConfigField(
                    "long",
                    "MINIMUM_DELAY",
                    "3"
            )
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        debug {
            java.srcDirs += 'src/debug/java'
        }
        release {
            java.srcDirs += 'src/release/java'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha03'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha03'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.61"

    implementation 'com.google.dagger:dagger:2.25.2'
    kapt 'com.google.dagger:dagger-compiler:2.25.2'

    implementation 'com.google.android.material:material:1.2.0-alpha03'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'

    implementation 'de.hdodenhof:circleimageview:3.0.1'

    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'

    implementation 'com.squareup.moshi:moshi-kotlin:1.9.2'
    implementation 'com.squareup.retrofit2:converter-moshi:2.6.2'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.9.2'

    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.2'

    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
}
